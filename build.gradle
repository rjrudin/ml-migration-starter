plugins {
	id "java"
	id "com.github.jk1.dependency-license-report" version "0.3.11"
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
	maven { url "http://developer.marklogic.com/maven2" }
}

dependencies {
	// Core dependencies needed for using Spring Batch, Spring JDBC, and marklogic-spring-batch
	compile "com.marklogic:marklogic-spring-batch-core:1.3.1"
	compile "com.marklogic:spring-batch-rdbms:1.3.0"

	// Using H2 as a sample database; can pull its JDBC driver from a Maven repository
	testRuntime "com.h2database:h2:1.4.193"

	// Can reference a local JDBC driver like this (Oracle, along with others, do not publish their JDBC driver to a public repo)
	// runtime files("./path/to/ojdbc-6.jar")

}

task migrate(type: JavaExec) {
	description = "Run the migration program"
	classpath = sourceSets.test.runtimeClasspath
	main = "com.marklogic.batch.rdbms.Main"
	args = [
		"--chunk", chunk,
		"--hosts", hosts,
		"--port", port,
		"--username", username,
		"--password", password,
		"--database", database,
		"--auth", auth,
		"--thread_count", thread_count,
		"--jdbc_driver", jdbc_driver,
		"--jdbc_url", jdbc_url,
		"--jdbc_username", jdbc_username,
		"--jdbc_password", jdbc_password,
		"--sql", sql,
		"--root_local_name", root_local_name,
		"--collections", collections,
		"--permissions", permissions,
		"--xcc", xcc,
		"--all_tables", all_tables,
		"--document_type", document_type
	]
}

task help(type: JavaExec) {
	description = "See a list of all supported command line arguments"
	classpath = sourceSets.main.runtimeClasspath
	main = "com.marklogic.batch.rdbms.Main"
	args = [
		"--help"
	]
}

task loadH2Data(type: JavaExec) {
	classpath = configurations.testRuntime
	main = "org.h2.tools.RunScript"
	args = [
			"-url", "jdbc:h2:file:./h2data/sample",
			"-user", "sa",
			"-script", "./src/test/resources/h2setup.sql"
	]
}

