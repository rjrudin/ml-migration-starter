plugins {
	id "java"
	id "com.github.jk1.dependency-license-report" version "0.3.11"
}

repositories {
	jcenter()
	mavenCentral()
	maven { url "http://developer.marklogic.com/maven2" }
}

dependencies {
	// Core dependencies needed for using Spring Batch, Spring JDBC, and marklogic-spring-batch
	compile "com.marklogic:marklogic-spring-batch-core:1.4.0"
	compile "com.marklogic:spring-batch-rdbms:1.4.0"

	// Using H2 as a sample database; can pull its JDBC driver from a Maven repository
	testRuntime "com.h2database:h2:1.4.193"

	// Can reference a local JDBC driver like this (Oracle, along with others, do not publish their JDBC driver to a public repo)
	// runtime files("./path/to/ojdbc-6.jar")

}

task migrate(type: JavaExec) {
	description = "Run the migration program"
	classpath = sourceSets.test.runtimeClasspath
	main = "com.marklogic.batch.rdbms.Main"
	args = [
		"--chunk", 50,
		"--thread_count", 4,
		"--collections", "raw",
		"--permissions", "rest-reader,read,rest-writer,update",
		"--all_tables", "true",
		"--root_local_name", "_tableName",
		"--output_uri_prefix", "/h2/",
		"--uri_id", "ID",
		"--document_type", "xml"
	]
}

task help(type: JavaExec) {
	description = "See a list of all supported command line arguments"
	classpath = sourceSets.main.runtimeClasspath
	main = "com.marklogic.batch.rdbms.Main"
	args = [
		"--help"
	]
}

task loadH2Data(type: JavaExec) {
	classpath = configurations.testRuntime
	main = "org.h2.tools.RunScript"
	args = [
			"-url", "jdbc:h2:file:./src/test/resources/h2/sample",
			"-user", "sa",
			"-script", "./src/test/resources/h2setup.sql"
	]
}

migrate.dependsOn loadH2Data
