apply plugin: 'java'

repositories {
	jcenter()
	maven { url "http://developer.marklogic.com/maven2" }
	flatDir {
		dirs 'lib'
	}
}

configurations {
	all {
		exclude module: 'commons-logging'
		exclude module: 'log4j'
	}
}

dependencies {
	compile "com.marklogic:marklogic-spring-batch-core:0.10.1"
	compile "com.marklogic:ml-javaclient-util:2.12.0"
	compile "org.springframework:spring-jdbc:4.3.5.RELEASE"

	// Example of pulling MySQL's JDBC driver from a public Maven repo
	//runtime "mysql:mysql-connector-java:5.1.6"
	runtime name: "ucanaccess-4.0.1"
	runtime name: "jackcess-2.1.6"
	runtime name: "hsqldb"
	runtime name: "commons-lang-2.6"
	runtime name: "commons-logging-1.1.1"

	// Can reference a local JDBC driver like this (Oracle, along with others, do not publish their JDBC driver to a public repo)
	// runtime files("./path/to/ojdbc-6.jar")

	// Forcing Spring to use logback instead of commons-logging
	runtime "ch.qos.logback:logback-classic:1.1.8"
	runtime group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.22"
	runtime group: "org.slf4j", name: "slf4j-api", version: "1.7.22"
}

task migrate(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = "org.example.MigrationMain"
	args = [
		"--rootLocalName", rootLocalName,
		"--allTables", allTables,
		"--databaseVendor", "Microsoft",
		"--sql", sql,
		"--collections", collections,
		"--permissions", permissions,
		"--config", "org.example.MigrationConfig",
		"--chunk", chunk,
		"--hosts", hosts,
		"--port", port,
		"--username", username,
		"--password", password,
		"--database", database,
		"--auth", auth,
		"--threadCount", threadCount,
		"--jdbc_driver", jdbcDriver,
		"--jdbc_url", jdbcUrl,
		"--jdbc_username", jdbcUsername,
		"--jdbc_password", jdbcPassword,
		"--xcc", xcc
	]
}

task help(type: JavaExec) {
	description = "See a list of all supported command line arguments"
	classpath = sourceSets.main.runtimeClasspath
	main = "org.example.MigrationMain"
	args = [
		"--config", "org.example.MigrationConfig",
		"--help"
	]
}
