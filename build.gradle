plugins {
	id "java"
	id "com.github.jk1.dependency-license-report" version "0.3.11"
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
	maven { url "http://developer.marklogic.com/maven2" }
}

configurations {
	all {
		exclude module: 'commons-logging'
		exclude module: 'log4j'
	}
}

dependencies {
	// Core dependencies needed for using Spring Batch, Spring JDBC, and marklogic-spring-batch
	compile "com.marklogic:marklogic-spring-batch-core:1.3.1"
	compile "com.marklogic:spring-batch-rdbms:1.3.0"

	// Logging Library
	compile "org.slf4j:slf4j-api:1.7.25"
	compile "org.slf4j:slf4j-log4j12:1.7.25"
	compile "org.apache.logging.log4j:log4j-api:2.9.1"

	// Using H2 as a sample database; can pull its JDBC driver from a Maven repository
	testRuntime "com.h2database:h2:1.4.193"

	// Can reference a local JDBC driver like this (Oracle, along with others, do not publish their JDBC driver to a public repo)
	// runtime files("./path/to/ojdbc-6.jar")

}

task migrate(type: JavaExec) {
	description = "Run the migration program"
	classpath = sourceSets.main.runtimeClasspath
	main = "com.marklogic.batch.rdbms.Main"
	args = [
		"--chunk", chunk,
		"--hosts", hosts,
		"--port", port,
		"--username", username,
		"--password", password,
		"--database", database,
		"--auth", auth,
		"--thread_count", thread_count,
		"--jdbc_driver", jdbc_driver,
		"--jdbc_url", jdbc_url,
		"--jdbc_username", jdbc_username,
		"--jdbc_password", jdbc_password,
		"--sql", sql,
		"--root_local_name", root_local_name,
		"--collections", collections,
		"--permissions", permissions,
		"--xcc", xcc,
		"--all_tables", all_tables,
		"--document_type", document_type
	]
}

task help(type: JavaExec) {
	description = "See a list of all supported command line arguments"
	classpath = sourceSets.main.runtimeClasspath
	main = "org.example.MigrationMain"
	args = [
		"--config", "org.example.MigrationConfig",
		"--help"
	]
}

task setupH2(type: JavaExec) {
	description = "Setup a file-based instance of H2 with sample data; can remove this if not using H2"
	classpath = configurations.runtime
	main = "org.h2.tools.RunScript"
	args = [
		"-url", "jdbc:h2:file:./h2data/sample",
		"-user", "sa",
		"-password", "password",
		"-script", "./h2setup.sql"
	]
}
